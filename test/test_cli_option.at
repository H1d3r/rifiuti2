dnl Test suite for rifiuti2

AT_SETUP([Short Options])
AT_KEYWORDS([option])
_r2_chk_status_only([-?])
_r2_chk_status_only([-v])
_r2_chk_opt_status_only([-t :])
_r2_chk_opt_status_only([-n])
AT_CLEANUP

AT_SETUP([Long Options])
AT_KEYWORDS([option])
_r2_chk_status_only([--help-all])
_r2_chk_opt_status_only([--delimiter=:])
_r2_chk_opt_status_only([--no-heading])
AT_CLEANUP

AT_SETUP([Invalid options])
AT_KEYWORDS([option])
_r2_chk_opt_status_only([--invalid-option], [1])
AX_AT_CHECK_PATTERN([dnl
	$progf -l foobar $sample/INFO2-sample2], [1],, [dnl
Error parsing options: 'foobar' encoding is not supported by glib library .*

	https://.*
])
AT_CLEANUP

AT_SETUP([Duplicative options])
AT_KEYWORDS([option])
AT_CHECK([dnl
	$progf -l ASCII -l CP1252 $sample/INFO2-sample2], [1],, [dnl
Error parsing options: Multiple encoding options disallowed.
])
_r2_chk_opt_err([-t : -t ,], [1], [dnl
Error parsing options: Multiple delimiter options disallowed.
])
_r2_chk_opt_err([-o file1 -o file2], [1], [dnl
Error parsing options: Multiple output destinations disallowed.
])
AT_CLEANUP

AT_SETUP([Options with empty arg])
AT_KEYWORDS([option])
_r2_chk_opt_err([-o ""], [1], [dnl
Error parsing options: Empty output filename disallowed.
])
AT_CHECK([dnl
	$progf -l "" $sample/INFO2-sample2], [1],, [dnl
Error parsing options: Empty encoding option disallowed.
])
AT_CLEANUP

dnl TODO check stderr too
AT_SETUP([Wrong combinations])
AT_KEYWORDS([option])
_r2_chk_opt_status_only([-x -t :], [1])
_r2_chk_opt_status_only([-x -n], [1])
AT_CLEANUP

dnl TODO check stderr too
AT_SETUP([Err on no file args])
AT_KEYWORDS([file option])
_r2_chk_status_only([-x], [1])
AT_CLEANUP

dnl TODO check stderr too
AT_SETUP([Err on multiple file args])
AT_KEYWORDS([file option])
_r2_chk_status_only([foo bar baz], [1])
AT_CLEANUP

AT_SETUP([Escaped char in delimiter])
AT_KEYWORDS([option])
AT_CHECK([
	$awk '{ gsub("\t","\n\t"); print; }' < $sample/INFO2-sample1.txt > expout
	cd $sample
	$progf -t "\n\t" INFO2-sample1
],, [expout], [])
AT_CHECK([
	$awk '{ gsub("\t","\n\t"); print; }' < $sample/dir-sample1.txt > expout
	cd $sample
	$progd -t "\n\t" dir-sample1
],, [expout], [])
AT_CLEANUP
